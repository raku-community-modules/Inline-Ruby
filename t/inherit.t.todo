use Inline::Python;
use Test;

plan 8;

my $py = Inline::Python.new();

$py.run(q:heredoc/PYTHON/);
    class PyFoo:
        def __init__(self, foo=u"Python default"):
            self._foo = foo

        def test(self):
            return self.bar()

        def foo(self):
            return self._foo

        def bar(self):
            return u"Python"

        def test_inherited(self):
            return self.baz()

        def baz(self):
            return u"Python"

    class PyBar:
        def test(self):
            return self.qux
    PYTHON

class Bar does Inline::Python::LangParent['__main__', 'PyFoo'] {
    method bar() {
        return "Raku";
    }
}

is(Bar.new().test, 'Raku');
is(Bar.new().test_inherited, 'Python');
is(Bar.new().foo, 'Python default');

class Baz does Inline::Python::LangParent['__main__', 'PyFoo'] {
    method bar() {
        return "Raku!";
    }

}

is(Baz.new().test, 'Raku!');

class Qux does Inline::Python::LangParent['__main__', 'PyBar'] {
    method qux() {
        return "Raku!!";
    }

}

is((Qux.new().test)(), 'Raku!!');

# Test passing a Py object to the constructor of a Raku subclass

class RakuObjectCreator {
    method create($package, $parent) {
        ::($package).WHAT.new(parent => $parent);
    }
}

$py.run(q:heredoc/PYTHON/);
    def init_raku_object_creator(creator):
        global raku_object_creator
        raku_object_creator = creator
    PYTHON

$py.call('__main__', 'init_raku_object_creator', RakuObjectCreator.new);

$py.run(q:heredoc/PYTHON/);
    foo = Bar(None, u"injected")
    bar = raku_object_creator.create(u"Bar", foo)
    PYTHON
my $bar = $py.run('bar', :eval)[0];
is($bar.foo, 'injected');
is($bar.test, 'Raku');
is($bar.test_inherited, 'Python');

# vim: ft=raku
